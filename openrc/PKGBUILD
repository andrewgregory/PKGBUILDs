# Maintainer Andrew Gregory <andrew.gregory.8@gmail.com>

pkgname=openrc
pkgdesc='Dependency based init system that works with sysvinit.'
pkgver=0.11.4
pkgrel=2
url='http://www.gentoo.org/proj/en/base/openrc/'
license=('BSD')
backup=('etc/openrc/rc.conf')
arch=('i686' 'x86_64')
depends=('openrc-arch-services-git' 'openrc-sysvinit' 'net-tools')
source=(http://dev.gentoo.org/~williamh/dist/$pkgname-$pkgver.tar.bz2)
md5sums=('e15144aa512365ba5dbd216bd6c5ada3')

_makeargs="BRANDING='Arch Linux'"
_makeargs+=' MKPAM=pam'
_makeargs+=' MKSELINUX=no'
_makeargs+=' MKTERMCAP=ncurses'
_makeargs+=' PKG_PREFIX=""'
_makeargs+=' SYSCONFDIR=/etc/openrc' # avoid conflict with initscripts

package() {
    cd "$srcdir/$pkgname-$pkgver"

    # openrc's makefile needs flags passed here instead of in build()
    make DESTDIR="$pkgdir" $_makeargs install

    # standard inittab, conflicts with initscripts,
    # does not require openrc-sysvinit
    # install -m 644 "$srcdir/openrc/support/sysvinit/inittab" "$pkgdir/etc/inittab"

    # avoid initscripts conflict, requires openrc-sysvinit
    install -m 644 support/sysvinit/inittab "$pkgdir/etc/openrc/inittab"
}
