# Maintainer:  Andrew Gregory <andrew.gregory.8@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=openrc
pkgver=0.11.8
pkgrel=1
pkgdesc='Dependency based init system that works with sysvinit.'
arch=('i686' 'x86_64')
url='http://www.gentoo.org/proj/en/base/openrc/'
license=('BSD')
depends=('openrc-sysvinit' 'pam' 'sh')
optdepends=('openrc-arch-services-git: collection of services for Arch'
            'net-tools: for network service support')
backup=(etc/openrc/inittab
        etc/openrc/rc.conf
        etc/openrc/conf.d/{bootmisc,consolefont,dmesg,fsck,hostname,hwclock,keymaps}
        etc/openrc/conf.d/{killprocs,localmount,modules,netmount,network,staticroute}
        etc/openrc/conf.d/{tmpfiles,urandom})
source=(http://dev.gentoo.org/~williamh/dist/$pkgname-$pkgver.tar.bz2
        $pkgname.logrotate
        0001-Fix-shebangs-in-services-to-point-to-the-correct-loc.patch
        0001-add-SBINDIR-variable-to-sys.mk.patch
        0001-consolefont-use-setfont-to-save-the-current-font.patch
        0001-disable-default-prefix-services-if-MKPREFIX-yes.patch
        0001-do-not-use-SYSCONFDIR-for-binfmt.d.patch
        0001-include-BINDIR-and-SBINDIR-in-the-sanitized-PATH.patch
        0001-make-BINDIR-and-SBINDIR-available-to-input-files.patch
        0001-make-init.sh-and-init-early.sh-honor-SYSCONFDIR.patch)
md5sums=('d896371d533a22de4cf57c7371c4d6e8'
         'ede356beae529d1b16b769c9da70ad52'
         '7007b80a57afe251c6dd882ff6d1a6b0'
         'a6149b1a50f1c55e4d841e99b1f17e00'
         'd89271ecc5aea11cc2ee7e7dd5e98b57'
         '4b54aae6aa53f7c766282f37e0b05807'
         '96cc28d55d55abd9c7f206aa010fc962'
         '9da224b41e32eb0a98c2bcfb286f5b9d'
         '945e3d224cf989c58529a9328245f7fc'
         '6e5369a6d6114faef2280fcd9d85f179')

_makeargs=(BRANDING='Arch Linux')
_makeargs+=(MKPAM=pam)
_makeargs+=(MKSELINUX=no)
_makeargs+=(MKTERMCAP=ncurses)
_makeargs+=(PKG_PREFIX="")
_makeargs+=(LIBDIR=/usr/lib)
_makeargs+=(SHLIBDIR=/usr/lib)
_makeargs+=(LIBEXECDIR=/usr/libexec/rc)
_makeargs+=(BINDIR=/usr/bin)
_makeargs+=(SBINDIR=/usr/bin)
_makeargs+=(SYSCONFDIR=/etc/openrc) # avoid conflict with initscripts

prepare() {
    cd "$srcdir"/$pkgname-$pkgver

    # inittab does not honor SBINDIR
    sed -ie 's!/sbin/rc!/usr/bin/rc!g' support/sysvinit/inittab

    # Apply Arch compatibility patches
    for patch in "$srcdir"/*.patch; do
        patch -p1 -i "$patch"
    done
}

build() {
    cd "$srcdir"/$pkgname-$pkgver
    make "${_makeargs[@]}"
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

    make DESTDIR="$pkgdir" "${_makeargs[@]}" install

    # default path to inittab conflicts with initscripts
    #install -m 644 support/sysvinit/inittab "$pkgdir"/etc/inittab

    # avoid initscripts conflict, requires openrc-sysvinit
    install -m 644 support/sysvinit/inittab "$pkgdir/etc/openrc/inittab"

    # rotate boot log
    install -Dm0644 "$srcdir/$pkgname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"
}
