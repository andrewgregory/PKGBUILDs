# Maintainer:  Andrew Gregory <andrew.gregory.8@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=openrc-git
pkgver=20121121
pkgrel=1
pkgdesc='Dependency based init system that works with sysvinit.'
arch=('i686' 'x86_64')
url='http://www.gentoo.org/proj/en/base/openrc/'
license=('BSD')
depends=('openrc-sysvinit' 'pam' 'sh')
optdepends=('openrc-arch-services-git: collection of services for Arch'
            'net-tools: for network service support')
makedepends=('git')
provides=('openrc')
conflicts=('openrc')
backup=(etc/openrc/inittab
        etc/openrc/rc.conf
        etc/openrc/conf.d/{bootmisc,consolefont,dmesg,fsck,hostname,hwclock,keymaps}
        etc/openrc/conf.d/{killprocs,localmount,modules,netmount,network,staticroute}
        etc/openrc/conf.d/{tmpfiles,urandom})
source=(openrc.logrotate)
md5sums=('ede356beae529d1b16b769c9da70ad52')

_gitroot='git://git.overlays.gentoo.org/proj/openrc.git'
_gitname='openrc'

_makeargs=(BRANDING='Arch Linux')
_makeargs+=(MKPAM=pam)
_makeargs+=(MKSELINUX=no)
_makeargs+=(MKTERMCAP=ncurses)
_makeargs+=(PKG_PREFIX="")
_makeargs+=(SYSCONFDIR=/etc/openrc) # avoid conflict with initscripts

build() {
    cd "$srcdir"

    msg "Connecting to GIT server...."

    if [ -d "$srcdir/$_gitname" ] ; then
        cd "$_gitname"
        git pull origin
        msg "The local files are updated."
    else
        git clone "$_gitroot"
        cd "$_gitname"
    fi

    msg "GIT checkout done or server timeout"

    make "${_makeargs[@]}"
}

package() {
    cd "$srcdir/$_gitname"

    make DESTDIR="$pkgdir" "${_makeargs[@]}" install

    # default path to inittab conflicts with initscripts
    #install -m 644 support/sysvinit/inittab "$pkgdir"/etc/inittab

    # avoid initscripts conflict, requires openrc-sysvinit
    install -m 644 support/sysvinit/inittab "$pkgdir/etc/openrc/inittab"

    # rotate boot log
    install -Dm0644 "$srcdir/$_gitname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"
}
